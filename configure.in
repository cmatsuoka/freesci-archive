\
AC_PREREQ(2.13)
AC_INIT(acinclude.m4)
AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(freesci, 0.3.2-devel)

AC_PROG_CC
AC_LIBTOOL_DLOPEN
AM_PROG_LIBTOOL
AC_PROG_LIBTOOL
AC_CHECK_HEADERS(limits.h)
AC_CHECK_HEADERS(sys/uio.h)
AC_CHECK_HEADERS(sys/soundcard.h)
AC_CHECK_HEADERS(sys/time.h)
AC_CHECK_HEADERS(obstack.h)
AC_CHECK_HEADERS(dirent.h)
AC_CHECK_HEADERS(unistd.h)
AC_CHECK_HEADERS(fnmatch.h)
AC_CHECK_HEADERS(readline/readline.h, [	ac_readline="-lreadline" ], [ ac_readline="" ])
AC_SUBST(ac_readline)
AC_CHECK_HEADERS(readline/history.h)
AC_CHECK_HEADERS(getopt.h)
AC_CHECK_SIZEOF(short)
AC_CHECK_SIZEOF(int)
AC_CHECK_SIZEOF(long)
AC_CHECK_FUNCS(pipe socketpair fork sched_yield memfrob ffs getopt_long isblank usleep)
AM_PATH_ALSA(0.5.0, [AC_DEFINE(HAVE_ALSA)], [echo >/dev/null])
AC_PATH_GGI

AC_ARG_WITH(sdl, [  --without-sdl           Do not build the SDL driver])
if test x"$with_sdl" != xno; then
	AM_PATH_SDL(1.1.8, 	[
					AC_DEFINE(HAVE_SDL)
					ac_sdl_o="sdl_driver.la"
				],
				[
					ac_sdl_o=""
				])
else
	AC_MSG_CHECKING([for SDL])
	AC_MSG_RESULT([disabled]) # Well, we should give some kind of feedback...
	ac_sdl_so=""
fi

AC_PATH_CURSES
AC_PATH_XTRA

if test x"$have_x" = xyes; then
	AC_CHECK_XSHM
fi

AC_MSG_CHECKING(for Alpha hardware and gcc or ccc style __asm__)
AC_TRY_COMPILE([], [
#ifdef __DECC
#  define my_asm(x) asm (x)
#else
#  define my_asm(x) __asm__ (x)
#endif
	my_asm("zapnot \$31, \$31, \$31");
	return 0;
], [
	AC_MSG_RESULT(found.)
	ev6_cpu="-mcpu=ev6"
	AC_DEFINE(HAVE_ALPHA_EV6_SUPPORT)
],
[
	AC_MSG_RESULT(not present.);
	ev6_cpu=""
])
AC_SUBST(ev6_cpu)


ac_xlib_o=""
if test x"$have_x" = xyes; then
	if test x"$x_libraries" != xNONE; then
		if test x"$x_libraries" != x; then
			LIBS="-L$x_libraries $LIBS"
		fi
		AC_CHECK_LIB(Xi, XGrabDevice, [X_LIBS="$X_LIBS -lXi"])
		AC_CHECK_LIB(X11, XOpenDisplay, [X_LIBS="$X_LIBS -lX11"])

		ac_xlib_o="xlib_driver.la"
	fi
fi

dnl if test "no_x" != yes; then
dnl 	AC_PATH_GLX
dnl fi
AC_SUBST(ac_glx_libraries)

AC_SUBST(ac_xlib_o)
AC_SUBST(ac_sdl_o)
AC_SUBST(ac_ggi_o)

#AC_PATH_PNG
AC_SUBST(ac_png_includes)
AC_SUBST(ac_png_libraries)
AM_PROG_LEX
AC_PROG_YACC

AC_ARG_WITH(efence,
	[  --with-efence           link against libefence (for debugging)],
	[LIBS="$LIBS -lefence"],
	[LIBS="$LIBS -lc"])

AC_ARG_WITH(Wall,
	[  --with-Wall		  compile using -Wall],
	[CFLAGS="-Wall $CFLAGS"],
	[])

AM_WITH_DMALLOC

AC_C_PARAMETRIZED_BIGENDIAN

AC_SUBST(VERSION)

AC_OUTPUT(Makefile    \
	debian/Makefile \
	doc/Makefile \
        doc/misc/Makefile \
        doc/man/Makefile \
	src/Makefile  \
	src/scicore/Makefile \
	src/engine/Makefile \
	src/gfx/Makefile \
	src/gfx/drivers/Makefile \
	src/gfx/resource/Makefile \
	src/include/Makefile \
	src/include/win32/Makefile \
	src/tools/Makefile \
	src/win32/Makefile \
	src/sound/Makefile \
	freesci.spec)
